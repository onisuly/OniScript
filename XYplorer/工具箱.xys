"检查XYplorer更新|:refreshsus"
    $new_version = readurl("http://www.xyplorer.com/version.php");
    $cur_version = property("FileVersion", "<xy>");
    if( compare($new_version, $cur_version, v) == 0 ) {
        msg "你正在使用最新版本！";
    }
    else {
        if( confirm("已有新版本XYplorer $new_version，是否立即更新？", , , 4) ) {
            run """<xypath>\Oni\app\wget\bin\wget.exe"" -P<xypath>\Oni\update http://www.xyplorer.com/download/xyplorer_full_noinstall.zip", , 2, 0;
            #182;
            $bat = "
            taskkill /im XYplorer.exe /f<crlf>
            ".substr("<xypath>",,1).":"."<crlf>
            cd <xypath><crlf>
            ""Oni\app\7-Zip\7z.exe"" x -aoa ""Oni\update\xyplorer_full_noinstall.zip""<crlf>
            start <xyexe><crlf>
            rd /s /q %~dp0
            ";
            writefile("<xypath>\Oni\update\exec.bat", $bat);
            run "<xypath>\Oni\update\exec.bat", , , 0;
        }
        else {
            ;
        }
    }

"YouTube视频下载|:browsenetwork"
    $clipboard = <clipboard>;
    $content = "";
    if( regexmatches (<clipboard>, "http[s]?://www\.youtube\.com[^ ]+", , 0) ) {
        $content = $clipboard;
    }
    $link = input("请输入一个YouTube的链接", "YouTube视频地址" , $content);
    if( regexmatches ($link, "http[s]?://www\.youtube\.com[^ ]+", , 0) ) {
        run """<xypath>\Oni\app\youtube-dl\youtube-dl.exe"" --proxy 127.0.0.1:8087 $content", , , 1;
    }
    else {
        msg "下载链接错误！";
    }

"谷歌自定义搜索<tab>-->|:fore"
    $html = popupmenu("视频搜索;<xypath>\Oni\app\谷歌自定义搜索\视频搜索.html|音乐搜索;<xypath>\Oni\app\谷歌自定义搜索\音乐搜索.html");
    if( $html != "" ) {open $html};

"特殊重命名 - Url解码|:rename"
    if( exists(<curitem>) == 1 ) {
        $python = "import os,sys<crlf>from urllib import unquote<crlf>print( unquote(sys.argv[1]).decode('utf-8').encode('gb2312') )";
        $pythonpath = %temp%."\"."rename.py";
        writefile($pythonpath, $python);
        $newfilename = runret("cmd /c python $pythonpath <curbase>", , , 0);
        $newfilename = substr($newfilename, 0, -2);
        renameitem($newfilename, , 1);
        delete(0, 0, $pythonpath);
    }
    else {
        msg "文件不存在！";
    }

"-"

"查看注册信息|:info"
    echo "用户名：".getkey("Name", "Register").<crlf>."注册码：".getkey("Code", "Register");
    
"-"

"编辑脚本|:conf"
    Openwith "Notepad", , self("file");